# -*- mode: python; -*-

Import(
    [
        "env",
        "get_option",
        "has_option",
        "http_client",
        "jsEngine",
        "version_parts",
        "version_extra",
    ]
)

env = env.Clone()


def shouldBuildGRPC(myEnv):
    return myEnv.TargetOSIs("linux") and myEnv["ENABLE_GRPC_BUILD"]


if shouldBuildGRPC(env):
    env.InjectThirdParty(libraries=["grpc"])


if not has_option("noshell") and jsEngine:
    shellEnv = env.Clone()
    if env.TargetOSIs("windows"):
        shellEnv.Append(LIBS=["winmm"])

        generatedMongoManifest = shellEnv.Substfile(
            "mongo.manifest.in",
            SUBST_DICT=[
                ("@mongo_version_major@", version_parts[0]),
                ("@mongo_version_minor@", version_parts[1]),
                ("@mongo_version_patch@", version_parts[2]),
                ("@mongo_version_extra@", version_parts[3]),
                ("@mongo_version_extra_str@", version_extra),
            ],
        )
        shellEnv.Alias("generated-sources", generatedMongoManifest)
        shellEnv.Depends("mongo.res", generatedMongoManifest)

    mongo_shell = shellEnv.Program(
        target="mongo",
        source=[
            "mongo.cpp",
        ]
        + env.WindowsResourceFile("mongo.rc"),
        LIBDEPS=[
            # NOTE: Do not add new libdeps (public or private) here unless
            # required by the linker to satisfy symbol dependencies from
            # the files listed above in `sources`. If you need to add a
            # library to inject a static or mongo initializer to mongo,
            # please add that library as a private libdep of
            # mongo_initializers.
            "$BUILD_DIR/mongo/db/server_base",
            "mongo_main",
        ],
        AIB_COMPONENT="jstestshell",
        AIB_COMPONENTS_EXTRA=[
            "devcore",
            "dist-test",
            "integration-tests",
            "mongo",
            "serverless-test",
        ],
        # TODO(SERVER-95322) Remove sys deps after converted to Bazel
        SYSLIBDEPS=(
            [
                "winhttp" if env.TargetOSIs("windows") else "curl",
            ]
            if http_client == "on"
            else []
        )
        + (["sasl2"] if "sasl" in env.get("MONGO_ENTERPRISE_FEATURES", []) else [])
        + (["synchronization"] if env.ToolchainIs("msvc") else []),
    )

env.CppUnitTest(
    target="shell_test",
    source=[
        "kms_test.cpp" if get_option("ssl") == "on" else [],
        "shell_options_test.cpp",
        "shell_utils_test.cpp",
        "program_runner_test.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/mongo/util/signal_handlers",
        "kms" if get_option("ssl") == "on" else [],
        "shell_options_register",
        "shell_utils",
    ],
)
