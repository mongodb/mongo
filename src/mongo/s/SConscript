# -*- mode: python -*-

Import("env")

env.SConscript(
    dirs=[
        'catalog',
        'client',
        'commands',
        'query',
        'write_ops',
    ],
)

# Functionality for initializing global sharding state
env.Library(
    target='sharding_initialization',
    source=[
        'sharding_initialization.cpp',
        'sharding_egress_metadata_hook_for_mongos.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/executor/network_interface_factory',
        '$BUILD_DIR/mongo/executor/network_interface_thread_pool',
        '$BUILD_DIR/mongo/executor/thread_pool_task_executor',
        '$BUILD_DIR/mongo/s/catalog/replset/catalog_manager_replica_set',
        '$BUILD_DIR/mongo/s/catalog/replset/dist_lock_catalog_impl',
        '$BUILD_DIR/mongo/s/catalog/replset/replset_dist_lock_manager',
        'client/sharding_connection_hook',
        'coreshard',
        'cluster_last_error_info',
    ],
)

env.Library(
    target='chunk_version',
    source=[
        'chunk_version.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/common',
    ]
)

# Functionality shared between mongod and mongos
env.Library(
    target='common',
    source=[
        'chunk_diff.cpp',
        'migration_secondary_throttle_options.cpp',
        'move_chunk_request.cpp',
        'set_shard_version_request.cpp',
        'shard_key_pattern.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/client/connection_string',
        '$BUILD_DIR/mongo/db/common',
        '$BUILD_DIR/mongo/db/matcher/expressions',
        '$BUILD_DIR/mongo/db/ops/update_common',
        '$BUILD_DIR/mongo/db/query/lite_parsed_query',
        '$BUILD_DIR/mongo/db/query/query_planner',
        '$BUILD_DIR/mongo/db/repl/optime',
        '$BUILD_DIR/mongo/rpc/metadata',
        'catalog/catalog_types',
        'chunk_version',
    ]
)

env.Library(
    target='sharding_test_fixture',
    source=[
        'sharding_test_fixture.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/auth/authorization_manager_mock_init',
        '$BUILD_DIR/mongo/db/service_context_noop_init',
        '$BUILD_DIR/mongo/client/remote_command_targeter_mock',
        '$BUILD_DIR/mongo/executor/network_test_env',
        '$BUILD_DIR/mongo/executor/task_executor_pool',
        '$BUILD_DIR/mongo/executor/thread_pool_task_executor_test_fixture',
        '$BUILD_DIR/mongo/rpc/metadata',
        '$BUILD_DIR/mongo/s/catalog/dist_lock_manager_mock',
        '$BUILD_DIR/mongo/s/catalog/replset/catalog_manager_replica_set',
        '$BUILD_DIR/mongo/s/coreshard',
        '$BUILD_DIR/mongo/s/mongoscore',
        '$BUILD_DIR/mongo/util/clock_source_mock',
        '$BUILD_DIR/mongo/util/net/message_port_mock',
    ],
    LIBDEPS_TAGS=[
        # Depends on coreshard, but that would be circular
        'incomplete',
    ],
)

env.CppUnitTest(
    target='chunk_diff_test',
    source=[
        'chunk_diff_test.cpp',
    ],
    LIBDEPS=[
        'common',
    ]
)

env.CppUnitTest(
    target='sharding_request_types_test',
    source=[
        'chunk_version_test.cpp',
        'migration_secondary_throttle_options_test.cpp',
        'move_chunk_request_test.cpp',
        'set_shard_version_request_test.cpp',
    ],
    LIBDEPS=[
        'common',
    ]
)

#
# Implementations of components to perform cluster operations in mongos
#
# This is the glue code implementing the interfaces required by cluster ops
# in particular environments.
#
env.Library(
    target='cluster_ops_impl',
    source=[
        'chunk_manager_targeter.cpp',
        'cluster_write.cpp',
        'dbclient_shard_resolver.cpp',
    ],
    LIBDEPS=[
        'client/sharding_client',
        'write_ops/cluster_write_op',
        'write_ops/cluster_write_op_conversion',
        '$BUILD_DIR/mongo/base',
    ],
    LIBDEPS_TAGS=[
        # Circular with coreshard, below
        'incomplete',
    ],
)

env.CppUnitTest(
    target='chunk_manager_targeter_test',
    source=[
        'chunk_manager_targeter_test.cpp',
    ],
    LIBDEPS=[
        'coreshard',
        'mongoscore',
        '$BUILD_DIR/mongo/db/auth/authorization_manager_mock_init',
        '$BUILD_DIR/mongo/db/service_context_noop_init',
    ]
)

env.CppUnitTest(
    target='chunk_manager_tests',
    source=[
        'chunk_manager_tests.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/s/catalog/replset/catalog_manager_replica_set_test_fixture',
        'coreshard',
    ]
)

# This library contains sharding functionality used by both mongod and mongos. Certain tests,
# which exercise this functionality also link against it.
env.Library(
    target='coreshard',
    source=[
        # This is only here temporarily for auto-split logic in chunk.cpp.
        'balancer/balancer.cpp',
        'balancer/balancer_chunk_selection_policy.cpp',
        'balancer/balancer_chunk_selection_policy_impl.cpp',
        'balancer/balancer_configuration.cpp',
        'balancer/balancer_policy.cpp',
        'balancer/cluster_statistics.cpp',
        'balancer/cluster_statistics_impl.cpp',
        'catalog/catalog_cache.cpp',
        'chunk.cpp',
        'chunk_manager.cpp',
        'config.cpp',
        'grid.cpp',
        'sharding_egress_metadata_hook.cpp',
        'sharding_raii.cpp',
        'shard_util.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/s/query/cluster_cursor_manager',
        '$BUILD_DIR/mongo/executor/task_executor_pool',
        'catalog/catalog_types',
        'catalog/replset/catalog_manager_replica_set',
        'client/sharding_client',
        'common',
    ],
    LIBDEPS_TAGS=[
        'incomplete',
    ],
)

env.Library(
    target='cluster_last_error_info',
    source=[
        'cluster_last_error_info.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/client/connection_string',
        '$BUILD_DIR/mongo/rpc/metadata',
        '$BUILD_DIR/mongo/db/commands/server_status_core',
        '$BUILD_DIR/mongo/db/service_context', 
        '$BUILD_DIR/mongo/db/stats/timer_stats',
    ]
)

# This library is only used by the mongos execuable and any tests which require mongos runtime
# objects, such as the request processing pipeline or the balancer.
env.Library(
    target='mongoscore',
    source=[
        'cluster_cursor_stats.cpp',
        'mongos_options.cpp',
        's_only.cpp',
        's_sharding_server_status.cpp',
        'version_mongos.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/auth/authmongos',
        '$BUILD_DIR/mongo/client/parallel',
        '$BUILD_DIR/mongo/db/fts/ftsmongos',
        '$BUILD_DIR/mongo/db/stats/counters',
        '$BUILD_DIR/mongo/s/query/cluster_query',
        '$BUILD_DIR/mongo/util/concurrency/task',
        'cluster_last_error_info',
        'write_ops/cluster_write_op',
        'write_ops/cluster_write_op_conversion',
    ],
)

env.CppUnitTest(
    target='mongoscore_test',
    source=[
        'balancer/balancer_configuration_test.cpp',
        'balancer/balancer_policy_tests.cpp',
        'balancer/cluster_statistics_test.cpp',
        'shard_key_pattern_test.cpp',
    ],
    LIBDEPS=[
        'coreshard',
        'mongoscore',
        'sharding_test_fixture',
        '$BUILD_DIR/mongo/db/auth/authorization_manager_mock_init',
        '$BUILD_DIR/mongo/db/service_context_noop_init',
    ]
)

env.Library(
    target='serveronly',
    source=[
        'd_sharding_server_status.cpp',
        "d_state.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/mongo/db/commands/core",
        "$BUILD_DIR/mongo/db/concurrency/lock_manager",
        "$BUILD_DIR/mongo/db/query/query",
        "$BUILD_DIR/mongo/db/range_deleter",
        "$BUILD_DIR/mongo/db/s/metadata",
        "$BUILD_DIR/mongo/db/s/sharding",
        "$BUILD_DIR/mongo/executor/network_interface_factory",
        "coreshard",
    ],
    LIBDEPS_TAGS=[
        # Depends on symbols from files in serverOnlyFiles
        'incomplete',
    ],
)
