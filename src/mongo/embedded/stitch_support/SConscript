# -*- mode: python; -*-

import libdeps

Import("env")
Import("get_option")

env = env.Clone()

stitchSupportEnv = env.Clone()
stitchSupportEnv.AppendUnique(
    CPPDEFINES=[
        'STITCH_SUPPORT_COMPILING',
     ],
)

if get_option('link-model') == 'static':
    stitchSupportEnv.AppendUnique(
        CPPDEFINES=[
            'STITCH_SUPPORT_STATIC',
        ],
    )
elif get_option('link-model') == 'dynamic-sdk':
    stitchSupportEnv['LIBDEPS_SHLIBEMITTER'] = libdeps.make_libdeps_emitter(
        'SharedArchive',
        libdeps.dependency_visibility_honored
    )

# Please see the note in ../mongo_embedded/SConscript about how to
# interpret and adjust the current and compatibility versions.
stitchSupportEnv.AppendUnique(
    SHLINKFLAGS=[
        '$MONGO_EXPORT_FILE_SHLINKFLAGS',
    ],
)

stitchSupportTargets = stitchSupportEnv.Library(
    target='stitch_support',
    source=[
        'stitch_support.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/index/index_access_methods',
        '$BUILD_DIR/mongo/db/matcher/expressions',
        '$BUILD_DIR/mongo/db/ops/parsed_update',
        '$BUILD_DIR/mongo/db/query/collation/collator_factory_icu',
        '$BUILD_DIR/mongo/db/query/collation/collator_factory_interface',
    ],
    AIB_COMPONENT='stitch-support',
)

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        '$INSTALL_DIR/include/stitch_support/v1/stitch_support',
        source=['stitch_support.h'],
        AIB_COMPONENT='stitch-support',
        AIB_ROLE='dev',
    )

if get_option('link-model') != 'dynamic-sdk':
    stitchSupportTestEnv = env.Clone()
    unitTest = stitchSupportTestEnv.Program(
        target="stitch_support_test",
        source=[
            "stitch_support_test.cpp",
        ],
        LIBDEPS=[
            '$BUILD_DIR/mongo/unittest/unittest',
            'stitch_support',
        ],
        AIB_COMPONENT='stitch-support',
        AIB_COMPONENTS_EXTRA=[
            'test',
            'unittests',
        ],
    )

    env.RegisterUnitTest(unitTest[0]);
