# -*- mode: python; -*-

Import("env")

env = env.Clone()

env.Library(
    target="unittest",
    source=[
        'barrier.cpp',
        'bson_test_util.cpp',
        'death_test.cpp',
        'matcher.cpp',
        'matcher_core.cpp',
        'temp_dir.cpp',
        'unittest_helpers.cpp',
        'unittest.cpp',
        'unittest.idl',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/server_options_core',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
        '$BUILD_DIR/third_party/shim_pcrecpp',
    ],
    AIB_COMPONENT='unittests'
)

env.Library(
    target="unittest_main",
    source=[
        'unittest_main.cpp',
        'unittest_options.idl',
    ],
    LIBDEPS=[
        'unittest',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/commands/test_commands_enabled',
        '$BUILD_DIR/mongo/db/wire_version',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
    ],
    AIB_COMPONENT='unittests'
)

env.Library(
    target="integration_test_main",
    source=[
        'integration_test_main.cpp',
        'integration_test_main.idl',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        'unittest',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/client/connection_string',
        '$BUILD_DIR/mongo/db/commands/test_commands_enabled',
        '$BUILD_DIR/mongo/db/server_options',
        '$BUILD_DIR/mongo/db/server_options_base',
        '$BUILD_DIR/mongo/db/serverinit',
        '$BUILD_DIR/mongo/db/service_context',
        '$BUILD_DIR/mongo/db/wire_version',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
        '$BUILD_DIR/mongo/util/options_parser/options_parser_init',
    ],
    AIB_COMPONENT='integration-tests'
)

bmEnv = env.Clone()
bmEnv.InjectThirdParty(libraries=['benchmark'])
bmEnv.Library(
    target='benchmark_main',
    source=[
        'benchmark_main.cpp'
    ],
    LIBDEPS=[
        # benchmark_main must not be made to depend on additional libraries.
        # Additional requirements should be handled by the benchmark itself.
        # Examples can be found in fail_point_bm and commands_bm. See SERVER-60691.
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/third_party/shim_benchmark',
    ],
    AIB_COMPONENT='benchmarks'
)

env.Library(
    target='task_executor_proxy',
    source=[
        'task_executor_proxy.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/executor/task_executor_interface',
    ],
    AIB_COMPONENT='benchmarks'
)

env.CppUnitTest(
    target='unittest_test',
    source=[
        'unittest_test.cpp',
        'fixture_test.cpp',
        'temp_dir_test.cpp',
        'thread_assertion_monitor_test.cpp',
        'assert_that_test.cpp',
    ],
)

env.Benchmark(
    target='system_resource_canary_bm',
    source=[
        'system_resource_canary_bm.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/util/processinfo',
    ],
)
