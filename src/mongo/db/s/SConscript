# -*- mode: python -*-

Import("env")

env.Library(
    target='metadata',
    source=[
        'collection_metadata.cpp',
        'metadata_loader.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/db/common',
        '$BUILD_DIR/mongo/db/range_arithmetic',
        '$BUILD_DIR/mongo/s/common',
        '$BUILD_DIR/mongo/db/service_context',
    ]
)

env.Library(
    target='migration_types',
    source=[
        'migration_session_id.cpp',
        'start_chunk_clone_request.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/bson/util/bson_extract',
        '$BUILD_DIR/mongo/s/common',
    ],
)

env.Library(
    target='type_shard_identity',
    source=[
        'type_shard_identity.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/s/write_ops/batch_write_types',
        '$BUILD_DIR/mongo/bson/util/bson_extract',
        '$BUILD_DIR/mongo/s/common',
    ],
)

env.Library(
    target='sharding',
    source=[
        'active_migrations_registry.cpp',
        'chunk_move_write_concern_options.cpp',
        'collection_range_deleter.cpp',
        'collection_sharding_state.cpp',
        'metadata_manager.cpp',
        'migration_chunk_cloner_source.cpp',
        'migration_chunk_cloner_source_legacy.cpp',
        'migration_destination_manager.cpp',
        'migration_source_manager.cpp',
        'move_timing_helper.cpp',
        'operation_sharding_state.cpp',
        'sharded_connection_info.cpp',
        'sharding_initialization_mongod.cpp',
        'sharding_state.cpp',
        'sharding_state_recovery.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/bson/util/bson_extract',
        '$BUILD_DIR/mongo/db/common',
        '$BUILD_DIR/mongo/s/sharding_initialization',
        'metadata',
        'migration_types',
        'type_shard_identity',
    ],
    LIBDEPS_TAGS=[
        # Depends on symbols defined in files in serverOnlyFiles, and has
        # many other missing dependencies.
        'incomplete',
    ],
)

env.Library(
    target='commands',
    source=[
        'check_sharding_index_command.cpp',
        'cleanup_orphaned_cmd.cpp',
        'config/configsvr_add_shard_command.cpp',
        'config/configsvr_add_shard_to_zone_command.cpp',
        'config/configsvr_commit_chunk_migration_command.cpp',
        'config/configsvr_control_balancer_command.cpp',
        'config/configsvr_move_chunk_command.cpp',
        'config/configsvr_remove_shard_from_zone_command.cpp',
        'get_shard_version_command.cpp',
        'merge_chunks_command.cpp',
        'migration_chunk_cloner_source_legacy_commands.cpp',
        'migration_destination_manager_legacy_commands.cpp',
        'move_chunk_command.cpp',
        'set_shard_version_command.cpp',
        'sharding_server_status.cpp',
        'sharding_state_command.cpp',
        'split_chunk_command.cpp',
        'split_vector_command.cpp',
        'unset_sharding_command.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/bson/dotted_path_support',
        '$BUILD_DIR/mongo/db/commands',
        '$BUILD_DIR/mongo/db/db_raii',
        '$BUILD_DIR/mongo/db/dbhelpers',
        '$BUILD_DIR/mongo/db/repl/repl_coordinator_global',
        '$BUILD_DIR/mongo/s/serveronly',
        'metadata',
        'sharding',
    ],
    LIBDEPS_TAGS=[
        # Depends on getDeleter from db/serveronly
        'incomplete',
    ],
)

env.CppUnitTest(
    target='migration_types_test',
    source=[
        'migration_session_id_test.cpp',
        'start_chunk_clone_request_test.cpp',
    ],
    LIBDEPS=[
        'migration_types',
    ]
)

env.CppUnitTest(
    target='type_shard_identity_test',
    source=[
        'type_shard_identity_test.cpp'
    ],
    LIBDEPS=[
        'type_shard_identity',
    ]
)

env.CppUnitTest(
    target='sharding_metadata_test',
    source=[
        'metadata_loader_test.cpp',
        'collection_metadata_test.cpp',
    ],
    LIBDEPS=[
        'metadata',
        '$BUILD_DIR/mongo/db/auth/authorization_manager_mock_init',
        '$BUILD_DIR/mongo/db/common',
        '$BUILD_DIR/mongo/db/service_context_noop_init',
        '$BUILD_DIR/mongo/s/catalog/replset/sharding_catalog_test_fixture',
        '$BUILD_DIR/mongo/s/coreshard',
        '$BUILD_DIR/mongo/s/catalog/dist_lock_manager_mock',
        '$BUILD_DIR/mongo/s/sharding_test_fixture',
    ]
)

env.CppUnitTest(
    target='sharding_state_test',
    source=[
        'active_migrations_registry_test.cpp',
        'metadata_manager_test.cpp',
        'sharding_state_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/client/remote_command_targeter_mock',
        '$BUILD_DIR/mongo/db/serveronly',
        '$BUILD_DIR/mongo/executor/network_test_env',
        '$BUILD_DIR/mongo/executor/thread_pool_task_executor_test_fixture',
        '$BUILD_DIR/mongo/s/catalog/sharding_catalog_mock',
        '$BUILD_DIR/mongo/s/coreshard',
        '$BUILD_DIR/mongo/util/clock_source_mock',
        '$BUILD_DIR/mongo/util/net/message_port_mock',
    ],
)

env.CppUnitTest(
    target='collection_sharding_state_test',
    source=[
        'collection_sharding_state_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/client/remote_command_targeter_mock',
        '$BUILD_DIR/mongo/db/serveronly',
        '$BUILD_DIR/mongo/executor/network_test_env',
        '$BUILD_DIR/mongo/executor/thread_pool_task_executor_test_fixture',
        '$BUILD_DIR/mongo/s/catalog/sharding_catalog_mock',
        '$BUILD_DIR/mongo/s/coreshard',
        '$BUILD_DIR/mongo/util/clock_source_mock',
        '$BUILD_DIR/mongo/util/net/message_port_mock',
    ],
)

