# -*- mode: python -*-

Import("env")
Import("get_option")
Import("has_option")
Import("use_system_version_of_library")

env.Library(
    target='intrusive_counter',
    source=[
        'intrusive_counter.cpp',
        ],
    LIBDEPS=[
        'foundation',
        ]
    )

debuggerEnv = env.Clone()
if has_option("gdbserver"):
    debuggerEnv.Append(CPPDEFINES=["USE_GDBSERVER"])

debuggerEnv.Library(
    target='debugger',
    source=[
        'debugger.cpp',
        ],
    LIBDEPS=[]
    )

env.Library(
    target='decorable',
    source=[
        'decoration_container.cpp',
        'decoration_registry.cpp',
    ],
    LIBDEPS=[]
    )

env.CppUnitTest(
    target='decorable_test',
    source=[
        'decorable_test.cpp'
        ],
    LIBDEPS=[
        'decorable',
        ]
    )

env.Library(
    target='progress_meter',
    source=[
        'progress_meter.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/logger/logger',
    ],
)

env.CppUnitTest(
    target='progress_meter_test',
    source=[
        'progress_meter_test.cpp',
    ],
    LIBDEPS=[
        'progress_meter',
    ],
)

env.Library(
    target='md5',
    source=[
        'md5.cpp',
        'password_digest.cpp',
    ],
)

env.CppUnitTest(
    target="md5_test",
    source=[
        "md5_test.cpp",
        "md5main.cpp",
    ],
    LIBDEPS=[
        "md5",
    ],
)

env.Library(
    target='foundation',
    source=[
        "startup_test.cpp",
        "touch_pages.cpp",
        'assert_util.cpp',
        'concurrency/mutex.cpp',
        'concurrency/thread_pool.cpp',
        'exception_filter_win32.cpp',
        'file.cpp',
        'log.cpp',
        'platform_init.cpp',
        'text.cpp',
        'thread_safe_string.cpp',
        'time_support.cpp',
        'timer.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base/base',
        '$BUILD_DIR/mongo/logger/logger',
        '$BUILD_DIR/mongo/platform/platform',
        '$BUILD_DIR/mongo/stacktrace',
        '$BUILD_DIR/mongo/util/concurrency/synchronization',
        '$BUILD_DIR/mongo/util/concurrency/thread_name',
        '$BUILD_DIR/mongo/util/debugger',
        '$BUILD_DIR/third_party/shim_allocator',
        '$BUILD_DIR/third_party/shim_boost',
        '$BUILD_DIR/third_party/shim_tz',
    ],
)

env.CppUnitTest(
    target='text_test',
    source=[
        'text_test.cpp'
    ],
    LIBDEPS=[
        'foundation',
    ],
)

env.CppUnitTest(
    target='time_support_test',
    source=[
        'time_support_test.cpp',
    ],
    LIBDEPS=[
        'foundation',
    ],
)

env.Library(
    target='stringutils',
    source=[
        'base64.cpp',
        'hex.cpp',
        'stringutils.cpp',
    ],
)

env.CppUnitTest(
    target="stringutils_test",
    source=[
        "stringutils_test.cpp",
    ],
    LIBDEPS=[
        "stringutils",
    ],
)

env.Library(
    target="processinfo",
    source=[
        "processinfo.cpp",
        "processinfo_${TARGET_OS}.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/mongo/bson",
        "foundation",
    ],
)

env.CppUnitTest(
    target="processinfo_test",
    source=[
        "processinfo_test.cpp",
    ],
    LIBDEPS=[
        "processinfo",
    ],
)

env.Library(
    target="fail_point",
    source=[
        "fail_point.cpp",
        "fail_point_registry.cpp",
        "fail_point_service.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/mongo/bson",
        "foundation",
    ],
)

env.CppUnitTest(
    target="fail_point_test",
    source=[
        "fail_point_test.cpp",
    ],
    LIBDEPS=[
        "fail_point",
    ],
)

env.Library(
    target='background_job',
    source=[
        "background.cpp",
    ],
    LIBDEPS=[
        'concurrency/spin_lock',
    ],
)

env.CppUnitTest(
    target="background_job_test",
    source=[
        "background_job_test.cpp",
    ],
    LIBDEPS=[
        "background_job",
        "concurrency/synchronization",
        "net/network", # Temporary crutch since the ssl cleanup is hard coded in background.cpp
    ],
)

if get_option('allocator') == 'tcmalloc':
    tcmspEnv = env.Clone()

    if not use_system_version_of_library('tcmalloc'):

        # Add in the include path for our vendored tcmalloc.
        tcmspEnv.InjectThirdPartyIncludePaths('gperftools')

        # If our changes to tcmalloc are ever upstreamed, this should become set based on a top
        # level configure check, though its effects should still be scoped just to these files.
        tcmspEnv.Append(
            CPPDEFINES=[
                'MONGO_HAVE_GPERFTOOLS_GET_THREAD_CACHE_SIZE'
            ]
        )

    tcmspEnv.Library(
        target='tcmalloc_set_parameter',
        source=[
            'tcmalloc_server_status_section.cpp',
            'tcmalloc_set_parameter.cpp',
        ],
        LIBDEPS=[
            '$BUILD_DIR/mongo/db/server_parameters',
        ],
        LIBDEPS_DEPENDENTS=[
            '$BUILD_DIR/mongo/${PROGPREFIX}mongod${PROGSUFFIX}',
            '$BUILD_DIR/mongo/${PROGPREFIX}mongos${PROGSUFFIX}',
        ],
    )


env.Library(
    target='ntservice',
    source=[
        'ntservice.cpp',
    ],
    LIBDEPS=[
        'foundation',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
    ],
)

if env.TargetOSIs('windows'):
    env.CppUnitTest(
        target='ntservice_test',
        source=[
            'ntservice_test.cpp',
        ],
        LIBDEPS=[
            'ntservice',
        ],
        LIBS=[
            'shell32',
            env['LIBS']
        ],
        NO_CRUTCH=True,
    )

env.Library(
    target='ntservice_mock',
    source=[
        'ntservice_mock.cpp',
    ],
)

env.Library(
    target='mmap',
    source=[
        'mmap.cpp',
        'mmap_${TARGET_OS_FAMILY}.cpp',
    ],
    LIBDEPS=[
        'foundation',
    ],
)
